plugins {
    id 'com.github.johnrengelman.shadow' version '6.0.0'
    id 'io.franzbecker.gradle-lombok' version '4.0.0'
    id 'java'
    id 'maven-publish'
}

import io.franzbecker.gradle.lombok.task.DelombokTask

lombok {
    version = '1.18.12'
    sha256 = ''
}

repositories {
    mavenLocal()
    maven {
        url = uri('https://repo1.maven.org/maven2')
    }

    maven {
        url = uri('https://maven.pkg.github.com/bigbade/enchantmenttokens')
    }

    maven {
        url = uri('https://hub.spigotmc.org/nexus/content/repositories/snapshots/')
    }

    maven {
        url = uri('https://repo.aikar.co/content/groups/aikar/')
    }

    maven {
        url = uri('https://oss.sonatype.org/content/groups/public/')
    }
}

configurations {
    resolveImplementation
    resolveImplementation.extendsFrom(implementation)
}

task delombok(type: DelombokTask, dependsOn: compileJava) {
    ext.outputDir = file("${buildDir}/${delombok.name}")
    classpath += project.configurations.shadow
    classpath += project.configurations.resolveImplementation
    outputs.dir(outputDir)
    sourceSets.main.java.srcDirs.each {
        inputs.dir(it)
        args(it, "-d", outputDir)
    }
}

javadoc {
    dependsOn delombok

    source = file("${buildDir}/${delombok.name}")
    classpath = configurations.compileClasspath

    options {
        setMemberLevel JavadocMemberLevel.PUBLIC
        setAuthor true

        links "https://docs.oracle.com/javase/8/docs/api/"
    }
}

task javadocJar(type: Jar) {
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    dependsOn delombok

    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

sourceSets {
    test {
        compileClasspath += configurations.resolveImplementation
        compileClasspath += configurations.shadow
        runtimeClasspath += configurations.resolveImplementation
        runtimeClasspath += configurations.shadow
    }
}

dependencies {
    implementation fileTree(dir: 'lib', include: '*.jar')

    shadow 'com.google.code.findbugs:jsr305:3.0.2'
    shadow 'org.spigotmc:spigot-api:1.15.2-R0.1-SNAPSHOT'

    testImplementation 'junit:junit:4.13'
    testImplementation 'org.mockito:mockito-core:3.5.13'
    testImplementation 'org.powermock:powermock-module-junit4:2.0.7'
    testImplementation 'org.powermock:powermock-api-mockito2:2.0.7'

    shadow 'org.projectlombok:lombok:1.18.12'
    annotationProcessor 'org.projectlombok:lombok:1.18.12'

    testCompileOnly 'org.projectlombok:lombok:1.18.12'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.12'
}

shadowJar {
    archiveClassifier.set('')
    archiveBaseName.set('EnchantmentTokensAPI')

    relocate 'co.aikar', 'software.bigbade'

    exclude 'META-INF/**'
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

group = 'software.bigbade'
version = 'Alpha-1.0.0'
